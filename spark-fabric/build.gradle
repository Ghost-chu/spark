buildscript {
    repositories {
        jcenter()
        maven { url = "https://maven.fabricmc.net/" }
    }
    dependencies {
        classpath 'fabric-loom:fabric-loom.gradle.plugin:0.2.5-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.1'
}

apply plugin: 'fabric-loom'

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand (
                'pluginVersion': project.pluginVersion,
                'pluginDescription': project.pluginDescription
        )
        include 'fabric.mod.json'
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'fabric.mod.json'
    }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    minecraft "com.mojang:minecraft:1.14.4"
    mappings "net.fabricmc:yarn:1.14.4+build.5"
    modCompile "net.fabricmc:fabric-loader:0.4.8+build.159"
    modCompile "net.fabricmc.fabric-api:fabric-api:0.3.0+build.207"

    shade project(':spark-common')
}

shadowJar {
    archiveName = 'spark-fabric.jar'
    configurations = [project.configurations.shade]

    relocate 'okio', 'me.lucko.spark.lib.okio'
    relocate 'okhttp3', 'me.lucko.spark.lib.okhttp3'
    relocate 'net.kyori.text', 'me.lucko.spark.lib.text'
    relocate 'org.tukaani.xz', 'me.lucko.spark.lib.xz'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}
